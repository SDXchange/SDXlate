* EPIDEMIC MODEL WITH THREE AGE GROUPS 
NOTE 
FOR A=1,3=CHLDRN,ADLTS,ELDRLY 
NOTE 
L 	SUSC.K(A)=SUSC.J(A)+DT*(-INF.JK(A)) 
NOTE 		SUSCEPTIBLE POPULATIONS (PEOPLE) 
N 	SUSC(A)=ISUSC(A) 
T 	ISUSC(*)=1980/980/695 
NOTE 		INITIAL VALUES OF SUSC 
R 	INF.KL(A)=TSICK.K*CNTCTS.K(A)*FRSICK(A) 
NOTE 		INFECTION RATES (PEOPLE/DAY) 
NOTE 
A 	TSICK.K=SUM(SICK.K) 
NOTE 		TOTAL SICK (PEOPLE) 
L 	SICK.K(A)=SICK.J(A)+DT*(INF.JK(A)-CURE.JK(A)) 
NOTE 		SICK POPULATIONS (PEOPLE) 
N 	SICK(A)=ISICK(A) 
T 	ISICK(*)=10/5/5 
NOTE 		INITIAL VALUES OF SICK 
T 	FRSICK(*)=0.05/0.01/0.03 
NOTE 		FRACTION SICK (DIHENSIONLESS) 
A 	CNTCTS.K(A)=TABLE(TABCON(*,A),SUSC.K(A)/TOTAL(A),0,1,.2) 
NOTE 		SUSCEPTIBLES CONTACTED PER INFECTED PERSON 
NOTE 		PER DAY (PEOPLE/PERSON/DAY) 
T 	TABCON(*,CHLDRN)=0/2/3/3.5/3.8/4 
T 	TABCON(*,ADLTS)=0/1.5/2.3/2.6/2.85/3 
T 	TABCON(*,ELDRLY)=0/1/1.5/1.8/1.93/2 
NOTE 		TABLES FOR CNTCTS 
N 	TOTAL(A)=SUSC(A)+SICK(A)+RECOV(A) 
NOTE 		TOTAL POPULATIONS IN EACH AGE GROUP (PEOPLE) 
NOTE 
R 	CURE.KL(A)=SICK.K(A)/DUR(A) 
NOTE 		CURE RATES (PEOPLE/DAY) 
T 	DUR(*)=7/8/10 
NOTE 		DURATION OF DISEASE (DAYS) 
L 	RECOV.K(A)=RECOV.J(A)+DT*CURE.JK(A) 
NOTE 		RECOVERED POPULATIONS (PEOPLE) 
N 	RECOV(A)=IRECOV(A) 
T	IRECOV(*)=10/15/0 
NOTE 		INITIAL VALUES OF RECOV 
NOTE 
NOTE 		CONTROL STATEMENTS
NOTE 
SPEC DT=0.5/LENGTH=50/PRTPER=0/PLTPER=1
PLOT SICK(CHLDRN)=C,SICK(ADLTS)=A,SICK(ELDRLY)=0
PLOT SUSC(CHLDRN)=C,SUSC(ADLTS)=A,SUSC(ELDRLY)=0
